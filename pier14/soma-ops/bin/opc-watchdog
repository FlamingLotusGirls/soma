#!/usr/bin/perl
# vim:set ai sw=4 ts=4 et:

use strict;
use warnings;
$|++;

# If we don't see good frames being generated by the Python client after this
# number of second, restart the Python client.
my $frames_timeout = 60;

sub uptime {
    open my $fd, "/proc/uptime" or die "proc: $!\n";
    my $line = <$fd>;
    my $ts = (split /\s+/, $line)[0];
    close $fd;
    return $ts;
}

sub main {
    my $last_good = uptime;
    my $last_reset = uptime;

    open my $fd, "stdbuf --output=0 tail -n0 -F /var/log/daemon.log |"
        or die "fork: $!\n";

    while (<$fd>) {
        next unless m! launch-opc-server.* (\d+\.\d+) frames/second!;
        my $frames = $1;
        my $now = uptime;

        $last_good = $now
            if $frames > 10;

        if ($now - $last_good > $frames_timeout and $now - $last_reset > $frames_timeout) {
            print "Watchdog triggered, trying restart\n";
            system "systemctl try-restart opc-client.service";
            $last_reset = uptime;
            $last_good = uptime;
        }
    }
}

print "Startup\n";
main;

__END__
[136288.816804] black1 launch-opc-server[604]: 0.0 frames/second
[136293.825606] black1 launch-opc-server[604]: 0.0 frames/second
